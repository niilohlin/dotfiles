#!/usr/bin/env zsh


# The code at the top and the bottom of this file is the same as in completion.zsh.
# Refer to that file for explanation.
if 'zmodload' 'zsh/parameter' 2>'/dev/null' && (( ${+options} )); then
  __fzf_key_bindings_options="options=(${(j: :)${(kv)options[@]}})"
else
  () {
    __fzf_key_bindings_options="setopt"
    'local' '__fzf_opt'
    for __fzf_opt in "${(@)${(@f)$(set -o)}%% *}"; do
      if [[ -o "$__fzf_opt" ]]; then
        __fzf_key_bindings_options+=" -o $__fzf_opt"
      else
        __fzf_key_bindings_options+=" +o $__fzf_opt"
      fi
    done
  }
fi

'builtin' 'emulate' 'zsh' && 'builtin' 'setopt' 'no_aliases'

{
    if [[ -o interactive ]]; then
        __fzf_defaults() {
          # $1: Prepend to FZF_DEFAULT_OPTS_FILE and FZF_DEFAULT_OPTS
          # $2: Append to FZF_DEFAULT_OPTS_FILE and FZF_DEFAULT_OPTS
          echo "--height ${FZF_TMUX_HEIGHT:-40%} --bind=ctrl-z:ignore $1"
          command cat "${FZF_DEFAULT_OPTS_FILE-}" 2> /dev/null
          echo "${FZF_DEFAULT_OPTS-} $2"
        }

        __fzfcmd() {
          [ -n "${TMUX_PANE-}" ] && { [ "${FZF_TMUX:-0}" != 0 ] || [ -n "${FZF_TMUX_OPTS-}" ]; } &&
            echo "fzf-tmux ${FZF_TMUX_OPTS}" || echo "fzf"
        }

        # CTRL-R - Paste the selected command from history into the command line
        fzf-history-widget() {
          local selected num
          setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
          selected="$(printf '%1$s\t%2$s\000' "${(vk)history[@]}" |
            perl -0 -ne 'if (!$seen{(/^\s*[0-9]+\**\s+(.*)/, $1)}++) { s/\n/\n\t/gm; print; }' |
            FZF_DEFAULT_OPTS=$(__fzf_defaults "" "-n2..,.. --scheme=history --bind=ctrl-r:toggle-sort --highlight-line ${FZF_CTRL_R_OPTS-} --query=${(qqq)LBUFFER} +m --read0") \
            FZF_DEFAULT_OPTS_FILE='' $(__fzfcmd))"
          local ret=$?
          if [ -n "$selected" ]; then
            if num=$(awk '{print $1; exit}' <<< "$selected" | grep -o '^[1-9][0-9]*'); then
              zle vi-fetch-history -n $num
            else # selected is a custom query, not from history
              LBUFFER="$selected"
            fi
          fi
          zle reset-prompt
          return $ret
        }
        zle     -N            fzf-history-widget
        bindkey -M emacs '^R' fzf-history-widget
        bindkey -M vicmd '^R' fzf-history-widget
        bindkey -M viins '^R' fzf-history-widget
    fi

} always {
  eval $__fzf_key_bindings_options
  'unset' '__fzf_key_bindings_options'
}
